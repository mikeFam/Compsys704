ControllerCapLoader(
		input signal bottleAtPos3, pusherRetracted,pusherExtended,WPgripped,armAtSource,armAtDest,empty,request;
		output signal pusherExtend,vacOn,armSource,armDest, jobDone;
		input Integer signal mode;
		input signal pusherExtendM,vacOnM,armSourceM,armDestM;
		)
->
{		
	signal selectM, selectA;
	
	{//Manual
		while(true){
			await(selectM);
			abort(selectA){
				{while(true){present(pusherExtendM){emit pusherExtend;} pause;}}
				||
				{while(true){present(vacOnM){emit vacOn;} pause;}}
				||
				{while(true){present(armSourceM){emit armSource;} pause;}}
				||
				{while(true){present(armDestM){emit armDest;} pause;}}
			}
		}
	}	
	||
	{//Auto
		while(true){
			await(selectA);
			abort(selectM){
				await(bottleAtPos3);
				System.out.println("Hello1");
				abort(immediate (!bottleAtPos3)){
					await(request && !empty);
					System.out.println("Hello2");
					abort(pusherExtended){
						sustain pusherExtend;
					}
					pause;
					await(pusherRetracted);
					abort(armAtSource){
						sustain armSource;
					}
					pause;
					abort(armAtDest){
						{sustain vacOn;}
						||
						{await (WPgripped);
						sustain armDest;}
					}
					System.out.println("Hello4");
					sustain jobDone;
				}
			}
		}
	}
	||
	{//Mode select
		await(mode);
		while(true){
			if((Integer)#mode == 1){
				emit selectM;
			}
			else{
				emit selectA;
			}
			pause;
		}
	}
}