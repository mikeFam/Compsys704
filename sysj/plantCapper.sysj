PlantCapper(
		input signal cylPos5ZaxisExtend, gripperTurnRetract, gripperTurnExtend, capGripperPos5Extend, cylClampBottleExtend;
		input signal enable;
		output signal bottleAtPos4, gripperZAxisLowered, gripperZAxisLifted, gripperTurnHomePos, gripperTurnFinalPos;
		output signal bottleAtPos4E, gripperZAxisLoweredE, gripperZAxisLiftedE, gripperTurnHomePosE, gripperTurnFinalPosE;
		)
->
{
	signal bottleClamp, bottleNotClamp, capGrip, capNotGrip;
	{//ZAxis
		while(true){
			abort (immediate (cylPos5ZaxisExtend && enable)){
				System.out.println("gripperZAxisLifted");
				sustain gripperZAxisLifted;
			}
			await(!enable);
			abort(immediate (!cylPos5ZaxisExtend && enable)){
				System.out.println("gripperZAxisLowered");
				sustain gripperZAxisLowered;
			}
			await(!enable);
		}
	}
	||
	{//Gripper
		while(true){
			{
				abort (immediate (gripperTurnExtend && enable)){
					System.out.println("gripperTurnHomePos");
					sustain gripperTurnHomePos;
				}
				await(!enable);
				abort(immediate (gripperTurnRetract && enable)){
					System.out.println("gripperTurnFinalPos");
					sustain gripperTurnFinalPos;
				}
				await(!enable);
			}
			||
			{
				abort (immediate (capGripperPos5Extend && enable)){
					System.out.println("capNotGrip");
					sustain capNotGrip;
				}
				await(!enable);
				abort(immediate (!capGripperPos5Extend && enable)){
					System.out.println("capGrip");
					sustain capGrip;
				}
				await(!enable);
			}
		}
	}
	||
	{//Clamp
		while(true){
			abort (immediate (cylClampBottleExtend && enable)){
				System.out.println("bottleNotClamp");
				sustain bottleNotClamp;
			}
			await(!enable);
			abort(immediate (!cylClampBottleExtend && enable)){
				System.out.println("bottleClamp");
				sustain bottleClamp;
			}
			await(!enable);
		}
	}
	||
	{//Bottle
		while(true){
			sustain bottleAtPos4;
		}
	}
	||
	{// To viz
		{while(true){present(bottleAtPos4){emit bottleAtPos4E;} pause;}}
		||
		{while(true){present(gripperZAxisLowered){emit gripperZAxisLoweredE;} pause;}}
		||
		{while(true){present(gripperZAxisLifted){emit gripperZAxisLiftedE;} pause;}}
		||
		{while(true){present(gripperTurnHomePos){emit gripperTurnHomePosE;} pause;}}
		||
		{while(true){present(gripperTurnFinalPos){emit gripperTurnFinalPosE;} pause;}}
	}
}