PlantConveyor(
		input signal motConveyorOnOff;
		input signal enable, refill;
		output signal bottleAtPos1,bottleLeftPos5;
		output signal bottleAtPos1E, bottleLeftPos5E;
		)
->
{
	signal bottleDec, bottleAtLoad, bottleAt5, motorOn, motorOff, empty;
	int signal bottlePos;
	emit bottlePos(0);
	{//Conveyor
		while(true){
			abort (immediate (motConveyorOnOff && enable)){
				System.out.println("motorOff");
				sustain motorOff;
			}
			await(!enable);
			abort(immediate (!motConveyorOnOff && enable)){
				System.out.println("motorOn");
				sustain motorOn;
			}
			await(!enable);
		}
	}
	||
	{
		while(true){
		int data = #bottlePos;
		System.out.println("data = " + data);
			if((Integer)#bottlePos == 1){
				abort(bottleAt5){
					System.out.println("helloAt1");
					sustain bottleAtPos1;
				}
			}
			else if((Integer)#bottlePos == 2){
				abort(bottleAtLoad){
					System.out.println("helloAt5");
					sustain bottleLeftPos5;
				}
			}
			pause;
		}
	}
	||
	{
		/*
		 * Bottle positions
		 * 0 = at Load
		 * 1 = at Pos1
		 * 2 = at Pos5
		 */
		while(true){
			// At Load
			abort(empty){
				while(true){
					System.out.println("Load");
					emit bottleAtLoad;
					await(motorOn);
					// At pos 1
					emit bottlePos(1);
					System.out.println("1");
					await (enable && motorOn);
					emit bottlePos(2);
					emit bottleAt5;
					await (!enable);
					System.out.println("2");
					emit bottleDec;
				}
			}
			await(refill);
		}
	}
	||
	{
		int bottlecount = 5;
		while(true){
			present(bottleDec){
				if(bottlecount > 0)
					bottlecount--;
			}
			present(refill){
				bottlecount = 5;
			}
			if(bottlecount == 0)
			emit empty;
			pause;
		}
	}
	||
	{// To viz
		{while(true){present(bottleAtPos1){emit bottleAtPos1E;} pause;}}
		||
		{while(true){present(bottleLeftPos5){emit bottleLeftPos5E;} pause;}}
	}
}

