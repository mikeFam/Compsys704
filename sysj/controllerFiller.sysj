ControllerFiller(
		input signal bottleAtPos2, dosUnitEvac, dosUnitFilled, inletOff, injectorOff;
		output signal valveInjectorOnOff, valveInletOnOff, dosUnitValveRetract, dosUnitValveExtend, jobDone;
		input Integer signal mode;
		input signal valveInjectorOnOffM, valveInletOnOffM, dosUnitValveRetractM, dosUnitValveExtendM;
		)
->
{	
	signal selectM, selectA;

	{//Manual
		while(true){
			await(selectM);
			abort(selectA){
				{while(true){present(valveInjectorOnOffM){emit valveInjectorOnOff;} pause;}}
				||
				{while(true){present(valveInletOnOffM){emit valveInletOnOff;} pause;}}
				||
				{while(true){present(dosUnitValveRetractM){emit dosUnitValveRetract;} pause;}}
				||
				{while(true){present(dosUnitValveExtendM){emit dosUnitValveExtend;} pause;}}
			}
		}
	}	
	||
	{//Auto
		while (true){
		await(selectA);	
			abort(selectM){
				await(bottleAtPos2);
				abort(immediate (dosUnitFilled)){
					{
					sustain dosUnitValveRetract;}
					||
					{
					sustain valveInjectorOnOff;}
				}
				abort(immediate (dosUnitEvac)){
					{
					sustain dosUnitValveExtend;}
					|| 
					{
					sustain valveInletOnOff;}
					||
					{
					sustain jobDone;}
				}
			}	
		}
	}
	||
	{//Mode select
		await(mode);
		while(true){
			if((Integer)#mode == 1){
				emit selectM;
			}
			else{
				emit selectA;
			}
			pause;
		}
	}
}


